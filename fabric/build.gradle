plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

repositories {
    maven {
        url "https://maven.terraformersmc.com/releases/"
        content {
            includeGroup "com.terraformersmc"
        }
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"
    modApi ("me.shedaniel.cloth:cloth-config-fabric:$rootProject.cloth_config_version"){
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "net.fabricmc", module: "fabric-loader")
    }
    modApi ("com.terraformersmc:modmenu:$rootProject.mod_menu_version"){
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "net.fabricmc", module: "fabric-loader")
    }

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'mod_version', project.version
    inputs.property 'mod_id', project.mod_id
    inputs.property 'maven_group', project.maven_group
    inputs.property 'mod_class_name', project.mod_class_name
    inputs.property 'mixin_files', project.mixin_files


    inputs.property 'mod_display_name', project.mod_display_name
    inputs.property 'mod_description', project.mod_description
    inputs.property 'mod_authors', project.mod_authors
    inputs.property 'mod_credits', project.mod_credits
    inputs.property 'mod_license', project.mod_license
    inputs.property 'mod_public_url', project.mod_public_url
    inputs.property 'mod_sources', project.mod_sources
    inputs.property 'mod_issue_tracker_url', project.mod_issue_tracker_url

    filesMatching('fabric.mod.json') {
        expand mod_version: project.version,
                mod_id: mod_id,
                maven_group: maven_group,
                mod_class_name: mod_class_name,
                mixin_files: mixin_files,

                mod_description: mod_description,
                mod_display_name: mod_display_name,
                mod_authors: mod_authors,
                mod_credits: mod_credits,
                mod_license: mod_license,
                mod_public_url: mod_public_url,
                mod_sources: mod_sources,
                mod_issue_tracker_url: mod_issue_tracker_url
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
