plugins {
    id 'dev.architectury.loom' version '1.6-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = project.minecraft_version
}

// Mode info

def loadPropertiesFromFile(File file) {
    def props = new Properties()
    file.withInputStream { stream ->
        props.load(stream)
    }
    return props
}

// Liste des fichiers de propriétés à charger
def propertiesFile = file('mod.info.gradle.properties')

def combinedProperties = new Properties()

if (propertiesFile.exists()) {
    def props = loadPropertiesFromFile(propertiesFile)
    combinedProperties.putAll(props)
}


// Appliquer les propriétés combinées au projet
combinedProperties.each { key, value ->
    project.ext[key] = value
}


allprojects {
    group = project.maven_group
    version = project.mod_version
    ext {
        mod_id = project.archives_name
        mod_class_name = project.mod_class_name

        mod_display_name = project.mod_display_name
        mod_description = project.mod_description
        mod_authors = project.mod_authors
        mod_credits = project.mod_credits
        mod_license = project.mod_license
        mod_sources = project.mod_sources
        mod_issue_tracker_url = project.mod_issue_tracker_url
    }
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 8
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}

